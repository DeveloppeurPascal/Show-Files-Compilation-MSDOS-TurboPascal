program show_fnt;
{
Auteur: Patrick Premartin.
Date: 27/11/1991.
      30/11/1991. (1.0)
Langage: Turbo Pascal 6.0

But: Slide permettant de voir les fichiers de fontes graphiques *.FNT
     On passe le nom du fichier (r‚pertoire, et masques DOS accept‚s).
}
uses
    crt,
    dos,
    olf_software,
    u_code_clavier,
    u_m_util,
    u_parametre,
    u_fnt;

const
     num_vers : string [3] = '1.0';
     dat_vers : string [10] = '30/11/1991';

var
   dir1 : dirstr;
   name : namestr;
   ext : extstr;
   dir0,
   nom : string;
   fich_type : searchrec;
   res : integer;

procedure mode_texte;
begin
     directvideo := true;
     asm
        push ax
        mov ah, 0
        mov al, 3
        int 10h
        pop ax
     end;
end; { Mode_Texte }

procedure mode_graphique;
begin
     directvideo := false;
     asm
        push ax
        mov ah, 0
        mov al, 0eh
        int 10h
        pop ax
     end;
end; { Mode_Graphique }

procedure aff_character;
var
   i, j : byte;
begin
     ecr_mode := normal;
     for i := 1 to 16 do
         for j := 1 to 16 do
             begin
                  gotoxy (succ (pred (i) * 5), j);
                  write (pred (i) * 16 + pred (j) : 3, ':');
                  ecr_xy (succ (pred (i) * 5)+4, j);
                  ecr_char (chr (pred (i) * 16 + pred (j)));
             end;
         {endfor}
     {endfor}
     alert (fich_type.name, al_nothing);
end; { Aff_Character }

begin
     init_licence ('show');
     init_olf_message (mess_careware);
     init_ligne_olf_software ('Show FoNte files '+num_vers, '(c) Patrick Premartin '+dat_vers);
     if ((param_del ('/H')) or (paramcount = 0) or param_del ('/?'))
     then
         begin
              writeln ('Syntax:');
              writeln ('       SHOWFNT [[path]file_name[.FNT]] [/H]');
              writeln;
              writeln ('Can show you file_name.FNT if it exists. You can use * and ? as jokers.');
              writeln ('The extension .FNT is generated by an other Olf Software Program:');
              writeln ('Character Maker. (Please contact us...)');
              writeln;
              writeln ('Only use EGA, VGA or upper card...');
         end
     else
         if (paramcount > 0)
         then
             begin
                  fsplit (fexpand (paramstr (1)), dir1, name, ext);
                  nom := dir1+name+'.FNT';
                  getdir (0, dir0);
                  chdir (copy (dir1, 1, pred (length (dir1))));
                  findfirst (nom, archive, fich_type);
                  if (doserror = 0)
                  then
                      mode_graphique
                  else
                      alert ('No FNT files on this directory...', al_error);
                  {endif}
                  while (doserror = 0) do
                        begin
                             if confirm ('Show you: '+fich_type.name+' ?')
                             then
                                 if (fnt_register (fnt_load (fich_type.name), 1) = 0)
                                 then
                                     if (0 = fnt_select (1))
                                     then
                                         aff_character;
                                     {endif}
                                 {endif}
                             {endif}
                             res := fnt_select (0);
                             findnext (fich_type);
                        end;
                  {endwhile}
                  chdir (dir0);
                  mode_texte;
             end;
         {endif}
     {endif}
end.
