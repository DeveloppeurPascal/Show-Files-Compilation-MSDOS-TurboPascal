program show_mou;
{
Auteur: Patrick Premartin.
Date: 27/11/1991.
      30/11/1991. (1.0)
Langage: Turbo Pascal 6.0

But: Slide permettant de voir les fichiers de souris graphiques *.MOU
     On passe le nom du fichier (r‚pertoire, et masques DOS accept‚s).
}
uses
    crt,
    dos,
    olf_software,
    u_code_clavier,
    u_m_util,
    u_parametre,
    u_mou;

const
     num_vers : string [3] = '1.0';
     dat_vers : string [10] = '30/11/1991';

var
   dir1 : dirstr;
   name : namestr;
   ext : extstr;
   dir0,
   nom : string;
   fich_type : searchrec;
   res : integer;

procedure mode_texte;
begin
     directvideo := true;
     asm
        push ax
        mov ah, 0
        mov al, 3
        int 10h
        pop ax
     end;
end; { Mode_Texte }

procedure mode_graphique;
begin
     directvideo := false;
     asm
        push ax
        mov ah, 0
        mov al, 0dh
        int 10h
        pop ax
     end;
end; { Mode_Graphique }

begin
     init_licence ('show');
     init_olf_message (mess_careware);
     init_ligne_olf_software ('Show MOUse files '+num_vers, '(c) Patrick Premartin '+dat_vers);
     if ((param_del ('/H')) or (paramcount = 0) or (param_del ('/?')))
     then
         begin
              writeln ('Syntax:');
              writeln ('       SHOWMOU [[path]file_name[.MOU]] [/H]');
              writeln;
              writeln ('Can show you file_name.MOU if it exists. You can use * and ? as jokers.');
              writeln ('.MOU files are generated by Mouse Maker (from Olf Software). Contact us...');
              writeln;
              writeln ('Only use EGA, VGA or upper card...');
         end
     else
         if (paramcount > 0)
         then
             begin
                  fsplit (fexpand (paramstr (1)), dir1, name, ext);
                  nom := dir1+name+'.MOU';
                  getdir (0, dir0);
                  chdir (copy (dir1, 1, pred (length (dir1))));
                  findfirst (nom, archive, fich_type);
                  if (doserror = 0)
                  then
                      mode_graphique
                  else
                      alert ('No MOU files on this directory...', al_error);
                  {endif}
                  while (doserror = 0) do
                        begin
                             if confirm ('Show you: '+fich_type.name+' ?')
                             then
                                 if (mou_register (mou_load (fich_type.name), 1) = 0)
                                 then
                                     if (0 = mou_select (1))
                                     then
                                         alert (fich_type.name, al_nothing);
                                     {endif}
                                 {endif}
                             {endif}
                             res := mou_select (0);
                             findnext (fich_type);
                        end;
                  {endwhile}
                  chdir (dir0);
                  mode_texte;
             end;
         {endif}
     {endif}
end.
